import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'jacoco'
}

group = 'com.bank'
version = '0.0.1-MVP'
sourceCompatibility = '15'

ext {
	r2dbcPostgresVersion = '0.8.6.RELEASE'
	h2Version = '1.4.200'
	r2dbcH2Version = '0.8.4.RELEASE'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

compileJava {
	options.compilerArgs += ['--enable-preview']
}

compileTestJava {
	options.compilerArgs += ['--enable-preview']
}

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "io.r2dbc:r2dbc-postgresql:$r2dbcPostgresVersion"
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "com.h2database:h2:$h2Version"
	testImplementation "io.r2dbc:r2dbc-h2:$r2dbcH2Version"
}

test {
	useJUnitPlatform()
	jvmArgs(['--enable-preview'])
	failFast = true
	testLogging.showStandardStreams(true)
	testLogging.exceptionFormat(TestExceptionFormat.FULL)
	testLogging.events(TestLogEvent.STARTED, TestLogEvent.SKIPPED, TestLogEvent.PASSED, TestLogEvent.FAILED)
}

jacoco {
	toolVersion = "0.8.7-SNAPSHOT"
}

jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['com/bank/accountmanager/configuration',
										'com/bank/accountmanager/handler/exception',
										'com/bank/accountmanager/handler/request',
										'com/bank/accountmanager/handler/response',
										'com/bank/accountmanager/repository/model',
			])
		}))
	}
}